PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX OBA: <http://purl.obolibrary.org/obo/OBA_>

SELECT ?entity ?property ?value WHERE {
  ?entity a owl:Class .
  ?entity rdfs:label ?label .
  
  FILTER NOT EXISTS {
    ?entity owl:deprecated "true"^^xsd:boolean
 }
  FILTER NOT EXISTS {
    ?entity rdfs:subClassOf* OBA:0000001
 }
  FILTER(STRSTARTS(STR(?entity),"http://purl.obolibrary.org/obo/OBA_"))
  BIND(rdfs:subClassOf as ?property)
  BIND("Not a subclass of biological attribute" as ?value)
}